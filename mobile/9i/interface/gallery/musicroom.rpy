
init python:
    from __future__ import absolute_import
    from __future__ import division
    from __future__ import print_function
    from __future__ import unicode_literals

    renpy.store.music_tracks = [
        ("Kaleidoscope", "music/first_version/01.ogg"),
        ("Happy☆Happy SchoolLife", "music/first_version/04.ogg"),
        ("日だまりキャンパス", "music/first_version/05.ogg"),
        ("乙女たちの放课后", "music/first_version/06.ogg"),
        ("坂道", "music/first_version/07.ogg"),
        ("そらの彼方に", "music/first_version/08.ogg"),
        ("日曜日", "music/first_version/09.ogg"),
        ("微笑に魅せられて", "music/first_version/10.ogg"),
        ("テンポアップ", "music/first_version/12.ogg"),
        ("天然少女", "music/first_version/13.ogg"),
        ("Unidentified Flying Object", "music/first_version/14.ogg"),
        ("星天宫の庭で", "music/first_version/15.ogg"),
        ("真夏はふぇすてぃばる!", "music/first_version/16.ogg"),
        ("Whisper…", "music/first_version/17.ogg"),
        ("水面の月", "music/first_version/18.ogg"),
        ("迷い星", "music/first_version/19.ogg"),
        ("忧愁の暗", "music/first_version/22.ogg"),
        ("恋水", "music/first_version/23.ogg"),
        ("同じ星に生まれて", "music/first_version/24.ogg"),
        ("Mysterious Dream", "music/first_version/25.ogg"),
        ("白昼梦", "music/first_version/26.ogg"),
        ("ゆめ", "music/first_version/27.ogg"),
        ("まつろわぬ少女", "music/first_version/28.ogg"),
        ("小春日和", "music/first_version/29.ogg"),
        ("秋黴雨", "music/first_version/30.ogg"),
        ("小さくも强き心", "music/first_version/31.ogg"),
        ("星空に誓う", "music/first_version/33.ogg"),
        ("Eternal recurrence", "music/first_version/36.ogg"),
        ("叶漏れ日", "music/first_version/41.ogg"),
        ("真夜中の展望台", "music/first_version/42.ogg"),
        ("巫女舞", "music/first_version/47.ogg"),
        ("星空のメモリア", "music/first_version/61.ogg"),
        ("White Eternity", "music/second_version/op.mp3"),
        ("ゆびさきシンパシー", "music/second_version/133.ogg"),
        ("パニッシュメント！", "music/second_version/104.ogg"),
        ("ガーリッシュハイテンション！", "music/second_version/106.ogg"),
        ("garden party", "music/second_version/119.ogg"),
        ("いつまでも、みんなで", "music/second_version/115.ogg"),
        ("すべてを雪に染めて", "music/second_version/130.ogg"),
        ("雪のエルフィンリート", "music/second_version/101.ogg"),
        ("クオリアの想い", "music/second_version/111.ogg"),
        ("季節を抱きしめて", "music/second_version/158.ogg"),
        ("こなゆきの妖精 -everyday arrange-", "music/second_version/148.ogg"),
        ("永久に舞う、雪の詩", "music/second_version/113.ogg"),
        ("永久に舞う、雪の詩Ⅱ", "music/second_version/140.ogg"),
        ("舞う雪に輝いて", "music/second_version/102.ogg"),
        ("永遠(とわ)", "music/second_version/151.ogg"),
        ("雪のエルフィンリー", "music/second_version/128.ogg"),
        ("星天宮の庭で", "music/second_version/120.ogg"),
        ("その想い、インフィニティー", "music/second_version/152.ogg"),
        ("季節を抱きしめて -arrange-", "music/second_version/157.ogg"),
        ("白きセカイの因果律", "music/second_version/138.ogg"),
        ("優しさの結晶", "music/second_version/142.ogg"),
        ("悠久の旋律", "music/second_version/163.ogg"),
        ("White Eternity -eternity arrange-", "music/second_version/155.ogg"),
        ("蒼空に舞う、輝きのカケラ", "music/second_version/145.ogg"),
        ("黄昏のエチュード", "music/second_version/118.ogg"),
        ("RAPID PROGRESS", "music/second_version/137.ogg"),
        ("アストラル アリア", "music/second_version/159.ogg"),
        ("moon white", "music/second_version/129.ogg"),
        ("新たなる始まり", "music/second_version/114.ogg"),
        ("こなゆきの妖精", "music/second_version/103.ogg"),
        ("純白の境界線", "music/second_version/165.ogg"),
        ("ましろのそら", "music/second_version/117.ogg"),
        ("ゆめいろ☆ゆきいろ", "music/second_version/108.ogg"),
        ("縁(よすが)の夜", "music/second_version/105.ogg"),
        ("ヴァルキュリアの咆哮", "music/second_version/161.ogg"),
        ("まつろわぬ鼓動", "music/second_version/124.ogg"),
        ("インナーチャイルド", "music/second_version/135.ogg"),
        ("遙かなる星々の記憶", "music/second_version/154.ogg"),
        ("雪空のラメント", "music/second_version/125.ogg"),
        ("冬が終わる日まで", "music/second_version/131.ogg"),
        ("dream telepathy", "music/second_version/122.ogg"),
        ("Phantasmagoria", "music/second_version/146.ogg"),
        ("RedAlert", "music/second_version/126.ogg"),
        ("プレコグニション", "music/second_version/123.ogg"),
        ("Runic unlimited", "music/second_version/127.ogg"),
        ("雪幻のエリクシア", "music/second_version/164.ogg"),
        ("vivid white school", "music/second_version/116.ogg"),
        ("サイキックエフェクト", "music/second_version/150.ogg"),
        ("フェアリーテイル", "music/second_version/139.ogg"),
        ("雪空に歌う、乙女たち", "music/second_version/107.ogg"),
        ("能力者たち", "music/second_version/149.ogg"),
        ("エルフィンの休日", "music/second_version/112.ogg")
    ]

    store.musicroom = musicroom = MusicRoom(fadeout=1.0, loop=True)
    for title, path in store.music_tracks:
        musicroom.add(path, always_unlocked=True)

screen musicroom():
    tag menu
    zorder +2

    on "replace" action [
        Function(renpy.show, "gallery_bg blur", behind=["menu"], layer="screens"),
        Play(channel="audio", file="9i/interface/paper_slide.ogg")
    ]

    on "show" action Show("musicroom_impl")
    on "replace" action Show("musicroom_impl")
    on "hide" action Hide("musicroom_impl")
    on "replaced" action Hide("musicroom_impl")

    frame style_prefix "musicroom_menu" at gallery_menu_inter:
        has hbox
        button:
            add "musicroom_menu_play_button"
            if renpy.music.get_playing() in {None, audio.main_menu_op}:
                action Function(musicroom.play, queue=False)
            else:
                action Function(musicroom.play, offset=1, queue=True)
            selected (renpy.music.get_playing() not in {None, audio.main_menu_op})
        button:
            add "musicroom_menu_stop_button"
            action musicroom.Stop()

    label _("{#gallery}MUSIC GALLERY") style "caption_label" at caption_inter


screen musicroom_impl(page=1):
    modal True
    zorder +1

    python:
        from __future__ import division

        from math import ceil

        page_total = int(ceil(len(music_tracks) / 22))

        del ceil

    default initial_track = renpy.music.get_playing() or audio.main_menu_op

    frame style_prefix "musicroom" at gallery_musicroom_inter:
        vbox style_prefix "musicroom" style "musicroom_left_vbox":
            for title, path in music_tracks[22 * (page - 1):22 * page - 11]:
                textbutton "[title]":
                    action [
                        musicroom.Stop(),
                        Play(
                            channel="music",
                            file=path,
                            fadeout=musicroom.fadeout,
                            loop=False)
                    ]
                    selected (renpy.music.get_playing() == path)
        vbox style_prefix "musicroom" style "musicroom_right_vbox":
            for title, path in music_tracks[22 * page - 11:22 * page]:
                textbutton "[title]":
                    action [
                        musicroom.Stop(),
                        Play(
                            channel="music",
                            file=path,
                            fadeout=musicroom.fadeout,
                            loop=False)
                    ]
                    selected (renpy.music.get_playing() == path)
        if page_total > 1:
            frame style_prefix "musicroom_page":
                if page > 1:
                    button style "musicroom_page_left_button":
                        action Show("musicroom_impl", page=page - 1)
                text "[page] / [page_total]"
                if page < page_total:
                    button style "musicroom_page_right_button":
                        action Show("musicroom_impl", page=page + 1)

    add "gallery_musicroom_player" at gallery_musicroom_inter
    vbox style_prefix "gallery_menu":
        textbutton _("Return"):
            action [
                Show("gallery"),
                Play(channel="audio", file="9i/interface/paper_slide_reverse.ogg"),
                Play(channel="music", file=initial_track, if_changed=True)
            ]
            keysym "game_menu"


init:
    transform gallery_musicroom_inter:
        on show:
            subpixel True yoffset scale(1620)
            ease 0.5 yoffset scale(0)
        on hide:
            subpixel True
            ease 0.5 yoffset scale(1620)

    image gallery_musicroom_player:
        "9i/interface/gallery/music/player.png"
        xcenter pscale(1710) ycenter pscale(829.125)
        imagescale(1080)

    image musicroom_menu_play_button:
        xcenter 0.5 ycenter 0.5
        imagescale(1080)
        "9i/interface/gallery/music/play.png"
        on idle:
            "9i/interface/gallery/music/play.png" with Dissolve(0.1, alpha=True)
        on selected_idle:
            "9i/interface/gallery/music/play hover.png" with Dissolve(0.1, alpha=True)
        on hover:
            "9i/interface/gallery/music/play hover.png" with Dissolve(0.1, alpha=True)
        on selected_hover:
            "9i/interface/gallery/music/play hover.png" with Dissolve(0.1, alpha=True)

    image musicroom_menu_stop_button:
        xcenter 0.5 ycenter 0.5
        imagescale(1080)
        "9i/interface/gallery/music/stop.png"
        on idle:
            "9i/interface/gallery/music/stop.png" with Dissolve(0.1, alpha=True)
        on hover:
            "9i/interface/gallery/music/stop hover.png" with Dissolve(0.1, alpha=True)

    style musicroom_text is text:
        # font "9i/FOT-ChiaroStd-B.otf"
        font "9i/fonts/浪漫雅圆.ttf"
        size scale(36)

    style musicroom_frame is default:
        xpos int(scale(67.5)) ypos int(scale(32.25))
        background imagescale(1080)(child="9i//interface/gallery/music/base.png")

    style musicroom_left_vbox is musicroom_vbox:
        xpos int(scale(100.5)) ypos int(scale(197.25))
        spacing scale(8)

    style musicroom_right_vbox is musicroom_left_vbox:
        xpos scale(732)

    style musicroom_button is default:
        ysize int(scale(54.75)) xsize int(scale(580.5))
        left_padding int(scale(76.5))
        hover_background imagescale(1080)(
            child="9i/interface/gallery/music/button hover.png")
        selected_background imagescale(1080)(
            child="9i/interface/gallery/music/button hover.png")
        hover_sound "9i/interface/click3.ogg"

    style musicroom_button_text is musicroom_text:
        ycenter 0.6
        color "#39241a"
        outlines [(scale(6.0), "#0000")]
        hover_color "#ffe4ae"
        hover_outlines [
            (scale(6.0), "#c2980607"),
            (scale(5.0), "#c298060b"),
            (scale(4.0), "#c298060f"),
            (scale(3.0), "#c2980613"),
            (scale(2.0), "#c2980617"),
            (scale(1.0), "#c298061f")
        ]

    style musicroom_page_frame is gallery_page_frame:
        xcenter pscale(1171.5) ycenter pscale(103.875)
        background imagescale(1080)(
            child="9i/interface/gallery/music/page base.png")

    style musicroom_page_text is gallery_page_text

    style musicroom_page_left_button is gallery_page_left_button:
        xcenter pscale(114) ycenter 0.5
        child imagescale(1080)(child="9i/interface/gallery/page left.png")

    style musicroom_page_right_button is gallery_page_right_button:
        xpos pscale(387.75) ycenter 0.5
        child imagescale(1080)(child="9i/interface/gallery/page right.png")

    style musicroom_menu_frame is default:
        xalign 1.0 ycenter pscale(287.625)
        xsize scale(387) ysize scale(91) xpadding scale(45)
        background Frame(
            imagescale(1080)(child="9i/interface/gallery/button.png"),
            top=scale(8))

    style musicroom_menu_hbox is hbox xalign 1.0 spacing scale(9) ycenter 0.5
    
    style musicroom_menu_button is default:
        xcenter 0.5 ycenter 0.5
        ysize scale(80) xsize scale(80)

